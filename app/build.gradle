plugins {
    id 'com.android.application'
}

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'https://gitlab.com/api/v4/projects/42572321/packages/maven'
    }
}

android {
    compileSdk 33
    buildToolsVersion = "31.0.0"

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'mozilla/public-suffix-list.txt', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0', 'META-INF/*.kotlin_module']
        }
    }

    defaultConfig {
        applicationId "com.astarivi.kaizoyu"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 2
        versionName "0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    buildFeatures {
        viewBinding true
    }

    namespace 'com.astarivi.kaizoyu'

    splits {
        abi {
            enable true
            reset()

            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            universalApk true
        }
    }
}

dependencies {
    // Core library
    implementation 'com.astarivi.kaizolib:KaizoLib:1.4'

    // External libraries (third party)
    implementation 'io.github.tonnyl:spark:0.1.0-alpha'
    implementation 'org.videolan.android:libvlc-all:4.0.0-eap6'
    implementation 'com.airbnb.android:lottie:6.0.0'
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    implementation 'com.github.bumptech.glide:recyclerview-integration:4.15.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.15.1'

    // Analytics
    implementation 'com.flurry.android:analytics:14.2.0'

    // Android X
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.work:work-runtime:2.8.0"
    implementation "androidx.room:room-runtime:2.5.0"
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.5.1'
    implementation 'androidx.palette:palette:1.0.0'
    annotationProcessor "androidx.room:room-compiler:2.5.0"

    // Logger
    implementation 'org.tinylog:tinylog-api:2.6.1'
    implementation 'org.tinylog:tinylog-impl:2.6.1'
    runtimeOnly 'org.tinylog:slf4j-tinylog:2.6.1'

    // Jackson
    // Don't update to 2.14 as Android minSdk support changes.
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.5'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.5'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.5'

    // HTTP
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'

    // Google others
    implementation 'com.google.android.material:material:1.9.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.2'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module('org.hamcrest:hamcrest-core:1.1') with module('junit:junit:4.13.2')
    }
}